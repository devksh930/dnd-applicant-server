plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
    id 'org.jetbrains.kotlin.plugin.spring' version "${kotlinVersion}"
    id 'org.jetbrains.kotlin.plugin.jpa' version "${kotlinVersion}"
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'org.asciidoctor.jvm.convert' version "${asciidoctorVersion}"
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'org.jetbrains.kotlinx.kover' version "${koverVersion}"
}

group = projectGroup
version = projectVersion

kotlin {
    jvmToolchain(21)
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file("build/generated-snippets")
}

dependencies {
    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Cache
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // JWT
    implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"

    // AWS
    implementation platform("software.amazon.awssdk:bom:${awsSdkVersion}")
    implementation 'software.amazon.awssdk:s3'

    // Documentation
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation("com.epages:restdocs-api-spec-mockmvc:${restdocsApiSpecVersion}") {
        exclude group: 'org.springframework.boot'
    }

    // Testing
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Kotest
    testImplementation "io.kotest:kotest-runner-junit5:${kotestVersion}"
    testImplementation "io.kotest:kotest-assertions-core:${kotestVersion}"
    testImplementation "io.kotest:kotest-property:${kotestVersion}"
    testImplementation "io.kotest.extensions:kotest-extensions-spring:${kotestSpringVersion}"

    // Mockito
    testImplementation "org.mockito.kotlin:mockito-kotlin:${mockitoKotlinVersion}"
}

// =============================================================================
// OpenAPI Spec Configuration
// =============================================================================
openapi3 {
    setServer(openapiServer)
    title = openapiTitle
    description = openapiDescription
    version = openapiVersion
    format = "json"
}

// =============================================================================
// Test Tasks
// =============================================================================
tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
    environment 'DOCKER_API_VERSION', '1.44'
}

tasks.register("restDocsTest", Test) {
    outputs.dir snippetsDir
    useJUnitPlatform {
        includeTags("restDocs")
    }
    jvmArgs("-XX:+EnableDynamicAgentLoading")
    finalizedBy 'asciidoctor', 'openapi3'
}

// =============================================================================
// Documentation Tasks
// =============================================================================
tasks.register("copyOpenApiToStatic", Copy) {
    description = "Copy openapi3.json to static resources for dev environment"
    from("build/api-spec") {
        include "openapi3.json"
    }
    into "src/main/resources/static/swagger"
    mustRunAfter "openapi3"
}

tasks.register("copyRestDocsToStatic", Copy) {
    description = "Copy REST Docs HTML to static resources for dev environment"
    dependsOn "asciidoctor"
    from("build/docs/asciidoc")
    into "src/main/resources/static/docs"
}

afterEvaluate {
    tasks.named("openapi3") {
        finalizedBy "copyOpenApiToStatic"
    }
    tasks.named("asciidoctor") {
        finalizedBy "copyRestDocsToStatic"
    }
}

tasks.named('asciidoctor') {
    dependsOn 'restDocsTest'
    mustRunAfter 'test'
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    baseDirFollowsSourceDir()
}

// =============================================================================
// Build Tasks
// =============================================================================
bootJar {
    dependsOn 'copyOpenApiToStatic', 'copyRestDocsToStatic'
    archiveFileName.set("app.jar")
}

clean {
    delete "src/main/resources/static/swagger"
    delete "src/main/resources/static/docs"
}

// =============================================================================
// Code Coverage
// =============================================================================
kover {
    reports {
        filters {
            excludes {
                classes(
                    "ac.dnd.server.DndServerApplication*",
                    "*.config.*",
                    "**.*Dto*",
                    "**.*Request*",
                    "**.*Response*"
                )
            }
        }
    }
}
