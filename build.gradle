plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.0.21'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
    id "com.epages.restdocs-api-spec" version "0.19.4"
    id 'org.jetbrains.kotlinx.kover' version '0.9.1'
}

group = 'ac.dnd'
version = '0.0.1-SNAPSHOT'

kotlin {
    jvmToolchain(21)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}
ext {
    snippetsDir = file("build/generated-snippets")
}

dependencies {
    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // Cache
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // AWS S3
    implementation platform('software.amazon.awssdk:bom:2.25.16')
    implementation 'software.amazon.awssdk:s3'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Test Dependencies
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.h2database:h2'

    // Kotest Dependencies
    testImplementation 'io.kotest:kotest-runner-junit5:5.8.0'
    testImplementation 'io.kotest:kotest-assertions-core:5.8.0'
    testImplementation 'io.kotest:kotest-property:5.8.0'

    // Mockito Kotlin
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.1.0'

    // Documentation - Springdoc OpenAPI (Swagger UI)
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0")
    // Documentation - Spring REST Docs & OpenAPI Spec
    testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")
    testImplementation("com.epages:restdocs-api-spec-mockmvc:${dependencyManagement.importedProperties['restdocs-api-spec.version'] ?: '0.19.4'}")
    // 버전 관리 활용

    asciidoctorExt("org.springframework.restdocs:spring-restdocs-asciidoctor")

}
tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading", "-Xshare:off", "-XX:+TieredCompilation", "-XX:TieredStopAtLevel=1")
    minHeapSize = "512m"
    maxHeapSize = "2g"
    maxParallelForks = (Runtime.runtime.availableProcessors() / 2) ?: 1
    environment 'DOCKER_API_VERSION', '1.44'
}

tasks.register("restDocsTest", Test) {
    outputs.dir snippetsDir
    useJUnitPlatform {
        includeTags("restDocs")
    }
    jvmArgs("-XX:+EnableDynamicAgentLoading", "-Xshare:off", "-XX:+TieredCompilation", "-XX:TieredStopAtLevel=1")
    minHeapSize = "512m"
    maxHeapSize = "2g"
    maxParallelForks = (Runtime.runtime.availableProcessors() / 2) ?: 1
    finalizedBy "asciidoctor"
    finalizedBy "openapi3"
}

tasks.named('asciidoctor') {
    dependsOn tasks.named("restDocsTest")
    mustRunAfter(tasks.named("test"))
    inputs.dir snippetsDir
    configurations "asciidoctorExt"
    baseDirFollowsSourceDir()
    attributes 'snippets': snippetsDir
}

bootJar {
    dependsOn asciidoctor
    from("swagger-ui") {
        into "BOOT-INF/classes/static/swagger"
    }
    from("${asciidoctor.outputDir}") {
        into "BOOT-INF/classes/static/docs"
    }
    from("build/api-spec") {
        into "BOOT-INF/classes/static/swagger"
    }
    archiveFileName.set("app.jar")
}

kover {
    reports {
        filters {
            excludes {
                classes(
                        "ac.dnd.server.DndServerApplication*",
                        "*.config.*"
                )
            }
        }
    }
}
